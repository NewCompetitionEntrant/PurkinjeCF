load_file("nrngui.hoc")

tstop = 15000

/* Parameters of the Purkinje Cell NEURON simulation model */
n_CF = 3
n_dend = 6
n_Stimulus=10

/* 
 * Create the soma, dendrites, climbing fibers and olivary nucleus
 * of the Purkinje Cell
 */
create PC_soma, PC_dend[n_dend], CF[n_CF], OlvNuc

/* Declaration of the objects that will be used in this file */
objref syn_CF[n_CF], syn[n_CF], nc[n_CF]
objref all_sub, CF_sub, PC_dend_sub
objref stim[n_Stimulus], nc_OlvNuc[n_Stimulus]

/*
 * This procedure connects the dendrites to the soma.
 * 2 dendrites are directly connected to the soma
 * and the other dendrites are connected to these
 * 2 dendrites to form branches
 *
 * Since this model simulates a neo-natal Purkinje Cell,
 * there are very few dendrites in this cell
 */
proc connect_dendrites() {
	connect PC_dend[0](0), PC_soma(1)
	connect PC_dend[1](0), PC_soma(1)
	connect PC_dend[2](0), PC_dend[0](1)
	connect PC_dend[3](0), PC_dend[1](1)
	connect PC_dend[4](0), PC_dend[0](1)
	connect PC_dend[5](0), PC_dend[1](1)
}

/* 
 * This procedure creates the synapses that connect the olivary nucleus 
 * to the climbing fibers
 */
proc connect_OlvNuc_to_CFs() {

	/* 
     * Stimulation signals generated by the olivary nucleus deep
     * inside the cerebellum
     */
	OlvNuc {
	stim[0] = new NetStim(0.5)
	stim[0].interval = 20
	stim[0].number = 100
	stim[0].start = 50
	stim[0].noise = 0

	stim[1] = new NetStim(0.5)
	stim[1].interval = 20
	stim[1].number = 100
	stim[1].start = 2150
	stim[1].noise = 0

	stim[2] = new NetStim(0.5)
	stim[2].interval = 20
	stim[2].number = 100
	stim[2].start = 4250
	stim[2].noise = 0

	stim[3] = new NetStim(0.5)
	stim[3].interval = 20
	stim[3].number = 100
	stim[3].start = 6500
	stim[3].noise = 0

	stim[4] = new NetStim(0.5)
	stim[4].interval = 20
	stim[4].number = 100
	stim[4].start = 10000
	stim[4].noise = 0

	stim[5] = new NetStim(0.5)
	stim[5].interval = 20
	stim[5].number = 100
	stim[5].start = 17500
	stim[5].noise = 0
	}

	/* 
     * Create the olivary nucleus and climbing fiber synapses.
     * These are standard exponentially decaying synapses.
     */
	CF[0] syn_CF[0] = new ExpSyn(0)
	CF[1] syn_CF[1] = new ExpSyn(0)
	CF[2] syn_CF[2] = new ExpSyn(0)

	/* 
     * Connect the climbing fibers with the olivary nucleus through the
     * synapses
     */
/*
	OlvNuc nc_OlvNuc[0] = new NetCon(stim[0], syn_CF[0], -20, 1, 0.5)
	OlvNuc nc_OlvNuc[1] = new NetCon(stim[1], syn_CF[1], -20, 1, 0.5)
	OlvNuc nc_OlvNuc[2] = new NetCon(stim[2], syn_CF[2], -20, 1, 0.5)
*/
	OlvNuc nc_OlvNuc[0] = new NetCon(stim[0], syn_CF[0], -20, 1, 1.0)

	OlvNuc nc_OlvNuc[1] = new NetCon(stim[1], syn_CF[0], -20, 1, 0.5) 
	OlvNuc nc_OlvNuc[2] = new NetCon(stim[2], syn_CF[0], -20, 1, 0.5) 
	OlvNuc nc_OlvNuc[3] = new NetCon(stim[3], syn_CF[0], -20, 1, 0.5) 
	OlvNuc nc_OlvNuc[4] = new NetCon(stim[4], syn_CF[0], -20, 1, 0.5) 
	OlvNuc nc_OlvNuc[5] = new NetCon(stim[5], syn_CF[0], -20, 1, 0.5) 

}

/* 
 * This procedure creates the synapses that connects the climbing fibers
 * to the soma of the Purkinje Cell
 */
proc connect_CFs_to_PC() {
/*
	PC_soma syn[0] = new ExpSyn(0.5)
	CF[0] nc[0] = new NetCon(&v(0.5), syn[0], -20, 1, 0.5)

	PC_soma syn[1] = new ExpSyn(0.5)
	CF[1] nc[1] = new NetCon(&v(0.5), syn[1], -20, 1, 0.5)

	PC_soma syn[2] = new ExpSyn(0.5)
	CF[2] nc[2] = new NetCon(&v(0.5), syn[2], -20, 1, 0.5)
*/

	PC_soma syn[0] = new AMPA_markov_syn(0.5)
	CF[0] nc[0] = new NetCon(&v(0.5), syn[0], -20, 1, 1.0)

/*
	PC_soma syn[1] = new AMPA_markov_syn(0.5)
	CF[1] nc[1] = new NetCon(&v(0.5), syn[1], -20, 1, 0.2)

	PC_soma syn[2] = new AMPA_markov_syn(0.5)
	CF[2] nc[2] = new NetCon(&v(0.5), syn[2], -20, 1, 0.5)
*/

}

/* 
 * This procedure orients the dendrites and climbing fibers
 * and shapes the Purkinje Cell
 */
proc basic_shape() {
	PC_soma {pt3dclear()
		pt3dadd(0, 0, 0, 1)
		pt3dadd(0, 2, 0, 1)
	}

	PC_dend[0] { pt3dclear()
		pt3dadd(0, 2, 0, 1)
		pt3dadd(-10, 7, 0, 1)
	}

	PC_dend[1] { pt3dclear()
		pt3dadd(0, 2, 0, 1)
		pt3dadd(8, 6, 0, 1)
	}

	PC_dend[2] { pt3dclear()
		pt3dadd(-10, 7, 0, 1)
		pt3dadd(-13, 11, 0, 1)
	}

	PC_dend[3] { pt3dclear()
		pt3dadd(8, 6, 0, 1)
		pt3dadd(4, 10, 0, 1)
	}

	PC_dend[4] { pt3dclear()
		pt3dadd(-10, 7, 0, 1)
		pt3dadd(-5, 10, 0, 1)
	}

	PC_dend[5] { pt3dclear()
		pt3dadd(8, 6, 0, 1)
		pt3dadd(13, 8, 0, 1)
	}

	CF[0] { pt3dclear()
		pt3dadd(0.5, 0, 0, 1)
		pt3dadd(4, -16, 0, 1)
	}

	CF[1] { pt3dclear()
		pt3dadd(-0.5, 0, 0, 1)
		pt3dadd(-4, -16, 0, 1)
	}

	OlvNuc { pt3dclear()
		pt3dadd(-0.1, -16, 0, 1)
		pt3dadd(0.1, -16, 0, 1)
	}
}

/* 
 * This is the top level procedure which calls lower level
 * procedures to create the Purkinje Cell topology.
 *
 * It connects the dendrites to the soma, the olivary
 * nucleus to the climbing fibers, the climbing fibers 
 * to the soma and generates the basic shape of the
 * Purkinje Cell
 */
proc topol() {
	connect_dendrites()
	connect_OlvNuc_to_CFs()
	connect_CFs_to_PC()
	basic_shape()
}

/* 
 * This procedure groups the different components of the Purkinje Cell
 * into subsections so that they can be accessed as one entity
 */
proc subsets() {local i
	all_sub = new SectionList()
	PC_soma all_sub.append()
	for i=0, n_dend-1 PC_dend[i] all_sub.append()
	for i=0, n_CF-1 CF[i] all_sub.append()
	OlvNuc all_sub.append()

	PC_dend_sub = new SectionList()
	for i=0, n_dend-1 PC_dend[i] PC_dend_sub.append()

	CF_sub = new SectionList()
	for i=0, n_CF-1 CF[i] CF_sub.append()
}

/* 
 * This procedure defines the geometry (length, diamter etc) of 
 * the vaious components of the Purkinje Cell
 */
proc geom() {
	PC_soma {
		diam = 18.8
		L = 18.8
	}

	for i = 0, n_dend-1 PC_dend[i] {
		diam = 3.0
		L = 3.0
	}  

	for i = 0, n_CF-1 CF[i] {
		diam = 18.8
		L = 50
	 }

	OlvNuc {
		diam = 18.8
		L = 18.8
	}
}

/*
 * This procedure defines the number of segments of each of
 * the components of the Purkinje Cell
 */
proc geom_nseg() {
	PC_soma {
		nseg = 1
	}

	forsec PC_dend_sub {
		nseg = 1
	}

	forsec CF_sub {
		nseg = 1
	}

	OlvNuc {
		nseg = 1
	}
}

/* 
 * This procedure defines the biophysical characteristics
 * of the various components of the Purkinje Cell
 *
 * It inserts the ion channels for each of the components
 */
proc biophys() {local i
	PC_soma {
		Ra = 123.0
		cm = 0.8

		insert pc_fast_K
		insert pc_medium_K
		insert pc_slow_K
		insert pc_ptype_ca
		insert pc_cation
		insert pc_BK
		insert pc_leak
		insert pc_cai
		insert pc_res_Na

		ek = -88
		ena = 60
		eca = 132
	}

	for i = 0, n_dend-1 PC_dend[i] {
		Ra = 123.0
    	insert pas
	}  

	for i = 0, n_CF-1 CF[i] {
		Ra = 123.0
		insert hh
	}  

	OlvNuc {
		Ra = 123.0
		insert pas
	}
}

/*
 * This is the topmost procedure which defines the Purkinje Cell's
 * geometry, biophysical properties, synapses and connections etc.
 */
proc celldef() { 
	topol()
	subsets()
	geom()
	biophys()
	geom_nseg()
}

/* Start the simulation */
access PC_soma
celldef()
run()




